plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

group 'me.vadim.util.conf'
version '1.0'

def basename = rootProject.name.concat('-').concat(project.name)

project.buildDir project.projectDir.path + '/target'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation project(':shared')
    implementation 'com.google.code.gson:gson:2.10'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.9.1'
}

jar {
    destinationDirectory.set(project.buildDir)
    archiveBaseName.set(basename)
    archiveFileName.set(archiveFileName.get().replaceAll('.jar', '-unshaded.jar'))//append '-unshaded' to the end
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

shadowJar {
    archiveBaseName.set(basename)
    destinationDirectory.set(project.buildDir)
    mergeServiceFiles()
    minimize()
}

tasks.register("sourceJar", Jar) {
    from(sourceSets.main.allSource)
    archiveClassifier.set("sources")
    classifier = "sources"
}

build {
    dependsOn(shadowJar)
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        maven(MavenPublication) {
            version = project.version.toString()
            groupId = project.group.toString()

            from components["java"]

            afterEvaluate {
                artifactId = basename
            }

            artifact(sourceJar)
        }
    }
}